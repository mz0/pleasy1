import org.gradle.internal.os.OperatingSystem

plugins {
    id 'bb.java-application-conventions'
}

// github.com/calimero-project/calimero-gui/blob/master/build.gradle
def swtGroupId = 'org.eclipse.platform'
def swtVersion = '3.122.0'  // 2022-12-06 version 4.956 Build-Jdk-Spec: 17
def swtArtifact = 'org.eclipse.swt.'

switch ( OperatingSystem.current() ) {
    case OperatingSystem.WINDOWS:
        swtArtifact += 'win32.win32.x86_64'
        break
    case OperatingSystem.MAC_OS:
        swtArtifact += 'cocoa.macosx.x86_64'
        break
    default:
        swtArtifact += 'gtk.linux.x86_64'
}

configurations.configureEach {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("${swtGroupId}:${swtArtifact}:${swtVersion}")
        }
    }
}

dependencies {
    implementation "${swtGroupId}:${swtArtifact}:${swtVersion}"
    implementation 'org.apache.commons:commons-text'
    implementation project(':utilities')
}

application {
    mainClass = 'bb.app.App'
}
