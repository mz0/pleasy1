plugins {
  id 'war'
  id "org.gretty" version "3.1.1"   // 4.x supports only Tomcat 10 and Jetty 11 (javax -> jakarta)
}

repositories {
    mavenCentral()
}

ext.flavor = project.hasProperty('flavor') ? project.flavor : 'ABF'
println "building ${flavor} flavor"

dependencies {
    implementation 'org.glassfish:javax.faces:2.3.9' // 2.2.9 downgrade FAIL
 // some parts in JSF need CDI see e.g. https://stackoverflow.com/a/18657532/228117
 // in this case it was h:dataTable + f:facet
    implementation 'javax.enterprise:cdi-api:1.2'
    implementation 'javax.servlet:jstl:1.2'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

    implementation project(':core')
}
switch (flavor) {
    case "AB": dependencies {
          implementation project(':serviceA')
          implementation project(':serviceB')
        }
        break
    case "AF": dependencies {
          implementation project(':serviceA')
          implementation project(':serviceF')
        }
        break
    default: dependencies {
          implementation project(':serviceA')
          implementation project(':serviceB')
          implementation project(':serviceF')
        }
}

gretty {
    servletContainer = 'jetty10'
    jetty10Version = '10.0.13' // 2022-12-07
    httpPort = 8080
}
